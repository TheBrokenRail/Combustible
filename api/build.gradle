plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.thebrokenrail.combustible'
version rootProject.gitVersionName

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    api 'com.squareup.moshi:moshi:1.14.0'
    api 'com.squareup.okhttp3:okhttp:4.11.0'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

tasks.register('generateMethods') {
    // Task Info
    group 'build'
    def dir = new File(projectDir, 'method-generator')
    def inputFiles = fileTree(dir) {
        exclude 'node_modules'
        exclude 'build'
    }
    inputs.files inputFiles
    def outputDir = new File(projectDir, 'src/main/java/com/thebrokenrail/combustible/api/method')
    outputs.dir outputDir
    // Commands To Run
    doLast {
        exec {
            workingDir dir
            commandLine 'npm', 'install'
        }
        exec {
            workingDir dir
            commandLine 'npm', 'start'
        }
    }
}
compileJava.dependsOn generateMethods

javadoc {
    options {
        addBooleanOption 'Xdoclint:all,-missing', true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/TheBrokenRail/Combustible'
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
}