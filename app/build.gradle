import io.github.classgraph.ClassGraph

buildscript {
    dependencies {
        classpath 'io.github.classgraph:classgraph:4.8.162'
    }

    ext {
        aboutLibraries_version = '10.8.3'
    }
}

plugins {
    id 'com.android.application'
    id 'org.ajoberstar.grgit' version '5.2.0'
    id 'com.mikepenz.aboutlibraries.plugin' version "${aboutLibraries_version}"
}

android {
    namespace 'com.thebrokenrail.combustible'
    compileSdk 34

    defaultConfig {
        applicationId 'com.thebrokenrail.combustible'
        minSdk 21
        targetSdk 34

        // Version Code
        def birthday = 1687752034L // The exact time the project was created!
        def gitVersionCode = (int) (grgit.head().dateTime.toEpochSecond() - birthday)
        versionCode gitVersionCode

        // Version Name
        def isDirty = !grgit.status().clean
        def gitVersionName = grgit.describe(tags: true, longDescr: isDirty, always: true)
        if (isDirty) {
            gitVersionName += '-dirty'
        }
        versionName gitVersionName

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        vectorDrawables.useSupportLibrary = true
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig = file("lint-config.xml")
    }

    def keystoreFile = file('../keystore.jks')
    if (keystoreFile.exists()) {
        signingConfigs {
            release {
                keyAlias 'key0'
                keyPassword 'password'
                storeFile keystoreFile
                storePassword 'password'
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

configurations.configureEach {
    exclude group: 'org.jetbrains', module: 'annotations-java5'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0-alpha02'
    implementation 'androidx.preference:preference:1.2.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'com.github.MikeOrtiz:TouchImageView:3.4'
    implementation 'androidx.browser:browser:1.6.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation project(':api')
    implementation "com.mikepenz:aboutlibraries-core-android:${aboutLibraries_version}"
    implementation("com.mikepenz:aboutlibraries:${aboutLibraries_version}") {
        // https://github.com/mikepenz/AboutLibraries/issues/881#issuecomment-1604480293
        exclude group: 'com.mikepenz', module: 'aboutlibraries-core'
    }
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Glide
    final def glide_version = '4.15.1'
    implementation "com.github.bumptech.glide:glide:${glide_version}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${glide_version}"
    implementation "com.github.bumptech.glide:annotations:${glide_version}"
    annotationProcessor "com.github.bumptech.glide:compiler:${glide_version}"

    // Markdown
    final def markwon_version = '4.6.2'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation "io.noties.markwon:syntax-highlight:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:html:$markwon_version"
    def prism4j_version = "2.0.0"
    implementation "io.noties:prism4j:$prism4j_version"
    annotationProcessor "io.noties:prism4j-bundler:$prism4j_version"
}

afterEvaluate { project ->
    def compileTask = project.tasks.compileDebugJavaWithJavac
    compileTask.doLast {
        def packageToScan = "com.thebrokenrail"
        def classPackageRoot = compileTask.destinationDir
        new ClassGraph()
                .overrideClasspath(classPackageRoot)
                .acceptPackages(packageToScan)
                .enableAllInfo()
                .scan()
                .withCloseable { scanResult ->
                    scanResult.getAllClasses().generateGraphVizDotFile(new File(buildDir, "classgraph.dot"))
                }
    }
}